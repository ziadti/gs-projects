# [Light Beam Simulator](https://docs.google.com/spreadsheets/d/1K_7DQ7b_dtYSsnGpCcr9c8ph5nwwDXfSoPpEWslQHL4/)

This is a simulation of [Day 16 of Advent of Code 2023](https://adventofcode.com/2023/day/16). 

## Demo

<img src="https://i.imgur.com/rjilLf2.gif" width="600">

## How I made it

This simulator is for the most part powered by this formula, which returns the list of coordinates visited by the beam of light. It works by keeping track of the positions to visit with their relative directions (up, down, left, right), starting from the top left and moving right. The list of positions is updated at each iteration based on the current value of the grid (`/`, `\`, `|`, `-`, `.`).

```py
=ARRAYFORMULA(LET(
   size,20,
   grid,grid!B2:U21,
   coord,COMPLEX(SEQUENCE(size),SEQUENCE(1,size)),
   path,REDUCE(
          {"1,i",FALSE},
          SEQUENCE(500),
          LAMBDA(acc,_,LET(
            to_visit,QUERY(acc,"select Col1 where Col2 = FALSE limit 1"),
            pos,REGEXEXTRACT(to_visit,"(.+),"),
            dir,REGEXEXTRACT(to_visit,",(.+)"),
            new_pos,IMSUM(pos,dir),
            updated_acc,{INDEX(acc,,1),IF(INDEX(acc,,1)=pos&","&dir,TRUE,INDEX(acc,,2))},
            cur,INDEX(grid,IMREAL(new_pos),IMAGINARY(new_pos)),
            IFNA(
              IF(OR(COUNTIF(coord,new_pos)=0, XLOOKUP(pos&","&dir,INDEX(acc,,1),INDEX(acc,,2),FALSE)=TRUE),
                updated_acc,
                IF(cur="/",VSTACK(updated_acc,{new_pos&","&IMSUB(0,COMPLEX(IMAGINARY(dir),IMREAL(dir))),FALSE}),
                  IF(cur="\",VSTACK(updated_acc,{new_pos&","&COMPLEX(IMAGINARY(dir),IMREAL(dir)),FALSE}),
                    IF(cur="|",VSTACK(updated_acc,{new_pos&",-1",FALSE;new_pos&",1",FALSE}),
                      IF(cur="-",VSTACK(updated_acc,{new_pos&",-i",FALSE;new_pos&",i",FALSE}),
                        VSTACK(updated_acc,{new_pos&","&dir,FALSE})))))),
              acc
            )
          ))
        ),
   visited,REGEXEXTRACT(INDEX(path,,1),"(.+),"),
   visited
 ))
```

The formula above returns the coordinates, to higlight the values relative to those coordinates I used the following auxiliary formula:

```py
=ARRAYFORMULA(COUNTIF(backend!A1:A,COMPLEX(SEQUENCE(20),SEQUENCE(1,20))))
```

Where `bakend!A1:A` is the array of visited coordinates and `COMPLEX(SEQUENCE(20),SEQUENCE(1,20))` is the 20x20 grid.

![image](https://github.com/user-attachments/assets/f47db153-1b33-4ddc-aa26-abc9763d4226)

Then I used the result generated by this auxiliary array in the following conditional formatting rule:

![image](https://github.com/user-attachments/assets/5f1afd52-cde7-4cc7-bd11-6c616baf00bd)
